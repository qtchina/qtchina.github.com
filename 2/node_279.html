<!DOCTYPE HTML>
<html>
    <head>
        <meta charset='utf8'>
        <title>HANDY ONE-LINE SCRIPTS FOR AWK (EN)</title>
        <link rel="stylesheet" href="../css/base.css" type="text/css" />
    </head>
<body>

<table width="80%" height="80 px" align="center">
  <tr>
    <td>
      <div id="branding"><strong><a href="/">
        <img src="../images/nullget_labs.gif" alt="www.qtchina.net " title="www.qtchina.net " id="logo" />
        <span>www.qtchina.net</span>            </a></strong></div>
      </div>
    </td>
  </tr>
</table>

<table width="80%" align="center">
  <tr>
    <td>
      <h2>HANDY ONE-LINE SCRIPTS FOR AWK (EN)</h2>
    </td>
  </tr>

  <tr>
    <td>
      发布： 2009-02-20 09:14
    </td>
  </tr>

  <tr>
    <td width="50%">
      <br />
HANDY ONE-LINE SCRIPTS FOR AWK                               30 April 2008<br />
Compiled by Eric Pement - eric [at] pement.org               version 0.27<br />
<br />
Latest version of this file (in English) is usually at:<br />
   http://www.pement.org/awk/awk1line.txt<br />
<br />
This file will also be available in other languages:<br />
   Chinese  - http://ximix.org/translation/awk1line_zh-CN.txt   <br />
<br />
USAGE:<br />
<br />
   Unix: awk '/pattern/ {print "$1"}'    # standard Unix shells<br />
DOS/Win: awk '/pattern/ {print "$1"}'    # compiled with DJGPP, Cygwin<br />
         awk "/pattern/ {print \"$1\"}"  # GnuWin32, UnxUtils, Mingw<br />
<br />
Note that the DJGPP compilation (for DOS or Windows-32) permits an awk<br />
script to follow Unix quoting syntax '/like/ {"this"}'. HOWEVER, if the<br />
command interpreter is CMD.EXE or COMMAND.COM, single quotes will not<br />
protect the redirection arrows (<, >) nor do they protect pipes (|).<br />
These are special symbols which require "double quotes" to protect them<br />
from interpretation as operating system directives. If the command<br />
interpreter is bash, ksh or another Unix shell, then single and double<br />
quotes will follow the standard Unix usage.<br />
<br />
Users of MS-DOS or Microsoft Windows must remember that the percent<br />
sign (%) is used to indicate environment variables, so this symbol must<br />
be doubled (%%) to yield a single percent sign visible to awk.<br />
<br />
If a script will not need to be quoted in Unix, DOS, or CMD, then I<br />
normally omit the quote marks. If an example is peculiar to GNU awk,<br />
the command 'gawk' will be used. Please notify me if you find errors or<br />
new commands to add to this list (total length under 65 characters). I<br />
usually try to put the shortest script first. To conserve space, I<br />
normally use '1' instead of '{print}' to print each line. Either one<br />
will work.<br />
<br />
FILE SPACING:<br />
<br />
 # double space a file<br />
 awk '1;{print ""}'<br />
 awk 'BEGIN{ORS="\n\n"};1'<br />
<br />
 # double space a file which already has blank lines in it. Output file<br />
 # should contain no more than one blank line between lines of text.<br />
 # NOTE: On Unix systems, DOS lines which have only CRLF (\r\n) are<br />
 # often treated as non-blank, and thus 'NF' alone will return TRUE.<br />
 awk 'NF{print $0 "\n"}'<br />
<br />
 # triple space a file<br />
 awk '1;{print "\n"}'<br />
<br />
NUMBERING AND CALCULATIONS:<br />
<br />
 # precede each line by its line number FOR THAT FILE (left alignment).<br />
 # Using a tab (\t) instead of space will preserve margins.<br />
 awk '{print FNR "\t" $0}' files*<br />
<br />
 # precede each line by its line number FOR ALL FILES TOGETHER, with tab.<br />
 awk '{print NR "\t" $0}' files*<br />
<br />
 # number each line of a file (number on left, right-aligned)<br />
 # Double the percent signs if typing from the DOS command prompt.<br />
 awk '{printf("%5d : %s\n", NR,$0)}'<br />
<br />
 # number each line of file, but only print numbers if line is not blank<br />
 # Remember caveats about Unix treatment of \r (mentioned above)<br />
 awk 'NF{$0=++a " :" $0};1'<br />
 awk '{print (NF? ++a " :" :"") $0}'<br />
<br />
 # count lines (emulates "wc -l")<br />
 awk 'END{print NR}'<br />
<br />
 # print the sums of the fields of every line<br />
 awk '{s=0; for (i=1; i<=NF; i++) s=s+$i; print s}'<br />
<br />
 # add all fields in all lines and print the sum<br />
 awk '{for (i=1; i<=NF; i++) s=s+$i}; END{print s}'<br />
<br />
 # print every line after replacing each field with its absolute value<br />
 awk '{for (i=1; i<=NF; i++) if ($i < 0) $i = -$i; print }'<br />
 awk '{for (i=1; i<=NF; i++) $i = ($i < 0) ? -$i : $i; print }'<br />
<br />
 # print the total number of fields ("words") in all lines<br />
 awk '{ total = total + NF }; END {print total}' file<br />
<br />
 # print the total number of lines that contain "Beth"<br />
 awk '/Beth/{n++}; END {print n+0}' file<br />
<br />
 # print the largest first field and the line that contains it<br />
 # Intended for finding the longest string in field #1<br />
 awk '$1 > max {max=$1; maxline=$0}; END{ print max, maxline}'<br />
<br />
 # print the number of fields in each line, followed by the line<br />
 awk '{ print NF ":" $0 } '<br />
<br />
 # print the last field of each line<br />
 awk '{ print $NF }'<br />
<br />
 # print the last field of the last line<br />
 awk '{ field = $NF }; END{ print field }'<br />
<br />
 # print every line with more than 4 fields<br />
 awk 'NF > 4'<br />
<br />
 # print every line where the value of the last field is > 4<br />
 awk '$NF > 4'<br />
<br />
STRING CREATION:<br />
<br />
 # create a string of a specific length (e.g., generate 513 spaces)<br />
 awk 'BEGIN{while (a++<513) s=s " "; print s}'<br />
<br />
 # insert a string of specific length at a certain character position<br />
 # Example: insert 49 spaces after column #6 of each input line.<br />
 gawk --re-interval 'BEGIN{while(a++<49)s=s " "};{sub(/^.{6}/,"&" s)};1'<br />
<br />
ARRAY CREATION:<br />
<br />
 # These next 2 entries are not one-line scripts, but the technique<br />
 # is so handy that it merits inclusion here.<br />
 <br />
 # create an array named "month", indexed by numbers, so that month[1]<br />
 # is 'Jan', month[2] is 'Feb', month[3] is 'Mar' and so on.<br />
 split("Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec", month, " ")<br />
<br />
 # create an array named "mdigit", indexed by strings, so that<br />
 # mdigit["Jan"] is 1, mdigit["Feb"] is 2, etc. Requires "month" array<br />
 for (i=1; i<=12; i++) mdigit[month[i]] = i<br />
<br />
TEXT CONVERSION AND SUBSTITUTION:<br />
<br />
 # IN UNIX ENVIRONMENT: convert DOS newlines (CR/LF) to Unix format<br />
 awk '{sub(/\r$/,"")};1'   # assumes EACH line ends with Ctrl-M<br />
<br />
 # IN UNIX ENVIRONMENT: convert Unix newlines (LF) to DOS format<br />
 awk '{sub(/$/,"\r")};1'<br />
<br />
 # IN DOS ENVIRONMENT: convert Unix newlines (LF) to DOS format<br />
 awk 1<br />
<br />
 # IN DOS ENVIRONMENT: convert DOS newlines (CR/LF) to Unix format<br />
 # Cannot be done with DOS versions of awk, other than gawk:<br />
 gawk -v BINMODE="w" '1' infile >outfile<br />
<br />
 # Use "tr" instead.<br />
 tr -d \r <infile >outfile            # GNU tr version 1.22 or higher<br />
<br />
 # delete leading whitespace (spaces, tabs) from front of each line<br />
 # aligns all text flush left<br />
 awk '{sub(/^[ \t]+/, "")};1'<br />
<br />
 # delete trailing whitespace (spaces, tabs) from end of each line<br />
 awk '{sub(/[ \t]+$/, "")};1'<br />
<br />
 # delete BOTH leading and trailing whitespace from each line<br />
 awk '{gsub(/^[ \t]+|[ \t]+$/,"")};1'<br />
 awk '{$1=$1};1'           # also removes extra space between fields<br />
<br />
 # insert 5 blank spaces at beginning of each line (make page offset)<br />
 awk '{sub(/^/, "     ")};1'<br />
<br />
 # align all text flush right on a 79-column width<br />
 awk '{printf "%79s\n", $0}' file*<br />
<br />
 # center all text on a 79-character width<br />
 awk '{l=length();s=int((79-l)/2); printf "%"(s+l)"s\n",$0}' file*<br />
<br />
 # substitute (find and replace) "foo" with "bar" on each line<br />
 awk '{sub(/foo/,"bar")}; 1'           # replace only 1st instance<br />
 gawk '{$0=gensub(/foo/,"bar",4)}; 1'  # replace only 4th instance<br />
 awk '{gsub(/foo/,"bar")}; 1'          # replace ALL instances in a line<br />
<br />
 # substitute "foo" with "bar" ONLY for lines which contain "baz"<br />
 awk '/baz/{gsub(/foo/, "bar")}; 1'<br />
<br />
 # substitute "foo" with "bar" EXCEPT for lines which contain "baz"<br />
 awk '!/baz/{gsub(/foo/, "bar")}; 1'<br />
<br />
 # change "scarlet" or "ruby" or "puce" to "red"<br />
 awk '{gsub(/scarlet|ruby|puce/, "red")}; 1'<br />
<br />
 # reverse order of lines (emulates "tac")<br />
 awk '{a[i++]=$0} END {for (j=i-1; j>=0;) print a[j--] }' file*<br />
<br />
 # if a line ends with a backslash, append the next line to it (fails if<br />
 # there are multiple lines ending with backslash...)<br />
 awk '/\\$/ {sub(/\\$/,""); getline t; print $0 t; next}; 1' file*<br />
<br />
 # print and sort the login names of all users<br />
 awk -F ":" '{print $1 | "sort" }' /etc/passwd<br />
<br />
 # print the first 2 fields, in opposite order, of every line<br />
 awk '{print $2, $1}' file<br />
<br />
 # switch the first 2 fields of every line<br />
 awk '{temp = $1; $1 = $2; $2 = temp}' file<br />
<br />
 # print every line, deleting the second field of that line<br />
 awk '{ $2 = ""; print }'<br />
<br />
 # print in reverse order the fields of every line<br />
 awk '{for (i=NF; i>0; i--) printf("%s ",$i);print ""}' file<br />
<br />
 # concatenate every 5 lines of input, using a comma separator<br />
 # between fields<br />
 awk 'ORS=NR%5?",":"\n"' file<br />
<br />
SELECTIVE PRINTING OF CERTAIN LINES:<br />
<br />
 # print first 10 lines of file (emulates behavior of "head")<br />
 awk 'NR < 11'<br />
<br />
 # print first line of file (emulates "head -1")<br />
 awk 'NR>1{exit};1'<br />
<br />
  # print the last 2 lines of a file (emulates "tail -2")<br />
 awk '{y=x "\n" $0; x=$0};END{print y}'<br />
<br />
 # print the last line of a file (emulates "tail -1")<br />
 awk 'END{print}'<br />
<br />
 # print only lines which match regular expression (emulates "grep")<br />
 awk '/regex/'<br />
<br />
 # print only lines which do NOT match regex (emulates "grep -v")<br />
 awk '!/regex/'<br />
<br />
 # print any line where field #5 is equal to "abc123"<br />
 awk '$5 == "abc123"'<br />
<br />
 # print only those lines where field #5 is NOT equal to "abc123"<br />
 # This will also print lines which have less than 5 fields.<br />
 awk '$5 != "abc123"'<br />
 awk '!($5 == "abc123")'<br />
<br />
 # matching a field against a regular expression<br />
 awk '$7  ~ /^[a-f]/'    # print line if field #7 matches regex<br />
 awk '$7 !~ /^[a-f]/'    # print line if field #7 does NOT match regex<br />
<br />
 # print the line immediately before a regex, but not the line<br />
 # containing the regex<br />
 awk '/regex/{print x};{x=$0}'<br />
 awk '/regex/{print (NR==1 ? "match on line 1" : x)};{x=$0}'<br />
<br />
 # print the line immediately after a regex, but not the line<br />
 # containing the regex<br />
 awk '/regex/{getline;print}'<br />
<br />
 # grep for AAA and BBB and CCC (in any order on the same line)<br />
 awk '/AAA/ && /BBB/ && /CCC/'<br />
<br />
 # grep for AAA and BBB and CCC (in that order)<br />
 awk '/AAA.*BBB.*CCC/'<br />
<br />
 # print only lines of 65 characters or longer<br />
 awk 'length > 64'<br />
<br />
 # print only lines of less than 65 characters<br />
 awk 'length < 64'<br />
<br />
 # print section of file from regular expression to end of file<br />
 awk '/regex/,0'<br />
 awk '/regex/,EOF'<br />
<br />
 # print section of file based on line numbers (lines 8-12, inclusive)<br />
 awk 'NR==8,NR==12'<br />
<br />
 # print line number 52<br />
 awk 'NR==52'<br />
 awk 'NR==52 {print;exit}'          # more efficient on large files<br />
<br />
 # print section of file between two regular expressions (inclusive)<br />
 awk '/Iowa/,/Montana/'             # case sensitive<br />
<br />
SELECTIVE DELETION OF CERTAIN LINES:<br />
<br />
 # delete ALL blank lines from a file (same as "grep '.' ")<br />
 awk NF<br />
 awk '/./'<br />
<br />
 # remove duplicate, consecutive lines (emulates "uniq")<br />
 awk 'a !~ $0; {a=$0}'<br />
<br />
 # remove duplicate, nonconsecutive lines<br />
 awk '!a[$0]++'                     # most concise script<br />
 awk '!($0 in a){a[$0];print}'      # most efficient script<br />
<br />
CREDITS AND THANKS:<br />
<br />
Special thanks to the late Peter S. Tillier (U.K.) for helping me with<br />
the first release of this FAQ file, and to Daniel Jana, Yisu Dong, and<br />
others for their suggestions and corrections.<br />
<br />
For additional syntax instructions, including the way to apply editing<br />
commands from a disk file instead of the command line, consult:<br />
<br />
  "sed & awk, 2nd Edition," by Dale Dougherty and Arnold Robbins<br />
  (O'Reilly, 1997)<br />
<br />
  "UNIX Text Processing," by Dale Dougherty and Tim O'Reilly (Hayden<br />
  Books, 1987)<br />
<br />
  "GAWK: Effective awk Programming," 3d edition, by Arnold D. Robbins<br />
  (O'Reilly, 2003) or at http://www.gnu.org/software/gawk/manual/<br />
<br />
To fully exploit the power of awk, one must understand "regular<br />
expressions." For detailed discussion of regular expressions, see<br />
"Mastering Regular Expressions, 3d edition" by Jeffrey Friedl (O'Reilly,<br />
2006).<br />
<br />
The info and manual ("man") pages on Unix systems may be helpful (try<br />
"man awk", "man nawk", "man gawk", "man regexp", or the section on<br />
regular expressions in "man ed").<br />
<br />
USE OF '\t' IN awk SCRIPTS: For clarity in documentation, I have used<br />
'\t' to indicate a tab character (0x09) in the scripts.  All versions of<br />
awk should recognize this abbreviation.<br />
<br />
#---end of file---<br />

    </td>
  </tr>
  <tr>
    <td>
      <br /><br />
      原文： <a href='http://nullget.sourceforge.net/?q=node/279'>http://nullget.sourceforge.net/?q=node/279</a>
    </td>
  </tr>

</table>

<p></p>
<p></p>
<p></p>
<table width="60%"><tr><td  align="center">
  <div>
    <span>Powered by <a href="http://nullget.sourceforge.net">zexport</a></span>  </div>
  </div>
  </td></tr></table>

<br />
<div>
<script>
  (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
  (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
  })(window,document,'script','//www.google-analytics.com/analytics.js','ga');

  ga('create', 'UA-51467019-1', 'qtchina.github.io');
  ga('send', 'pageview');

</script>

</div>
</body>
</html>
