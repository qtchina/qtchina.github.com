<!DOCTYPE HTML>
<html>
    <head>
        <meta charset='utf8'>
        <title>AWK单行脚本快速参考 (ZH)</title>
        <meta name="Keywords" content="Qt5,C/C++,Linux,PHP,Web架构,高性能,分布式,NullGet,WebNullGet,qtchina,Qt,下载软件,多线程下载软件,多协议下载软件,MMS下载,RTSP下载,HTTP下载,HTTPS下载,FTP下载,分块下载,在线视频下载软件，流媒体下载软件,Qt 4,China,Qt 4 Solution,Qt 4 解决方案" />
        <meta name="robots" content="all" />
        <meta name="googlebot" content="all" /> 
        <link rel="stylesheet" href="../css/base.css" type="text/css" />
    </head>
<body>

<table width="80%" height="80 px" align="center">
  <tr>
    <td>
      <div id="branding"><strong><a href="/">
        <img src="../images/nullget_labs.gif" alt="www.qtchina.net " title="www.qtchina.net " id="logo" />
        <span>www.qtchina.net</span>            </a></strong></div>
      </div>
    </td>
  </tr>
</table>

<table width="80%" align="center">
  <tr>
    <td>
      <h2>AWK单行脚本快速参考 (ZH)</h2>
    </td>
  </tr>

  <tr>
    <td>
      发布： 2009-02-20 09:15
    </td>
  </tr>

  <tr>
    <td width="50%">
      <br />
AWK单行脚本快速参考                                      2008年4月28日<br />
编辑： Eric Pement eric [at] pement.org                   版本 0.26<br />
翻译： 董一粟      yisudong [at] gmail.com<br />
   最新英文版本文件发布在以下地址：<br />
   http://www.pement.org/awk/awk1line.txt<br />
   最新中文翻译版本文件发布在以下地址：<br />
   http://ximix.org/translation/awk1line_zh-CN.txt<br />
   翻译问题欢迎联系我：<br />
   http://hi.baidu.com/ximix/blog/item/e9334cc2204fa330e5dd3bf2.html<br />
<br />
用法：<br />
<br />
    Unix:  awk '/pattern/ {print "$1"}'    # 标准 Unix shell环境<br />
 DOS/Win:  awk '/pattern/ {print "$1"}'    # DJGPP 可编译通过<br />
           awk "/pattern/ {print \"$1\"}"  # GnuWin32，UnxUtils，Mingw环境<br />
<br />
<br />
需要特别注意的是，DJGPP编译器可以允许awk脚本使用Unix的引号语法 <br />
'/like/ {"this"}'。但是，用户必须知道在DOS/Windows环境下，使用CMD.EXE或者<br />
COMMAND.COM程序的话，单引号并不能保护重定向符号（<, >）和管道（|）。 <br />
如果使用双引号的话，在DOS/CMD命令解释器下的特殊符号和他们的特殊含义都<br />
会被忽略。如果你的命令提示符是bash、ksh或者其他的Unix终端，单引号和双引号<br />
会沿用Unix标准的用法。<br />
<br />
同样，DOS/Win用户必须记住用，百分号（%）用来标记DOS/Win环境变量，<br />
如果想要在awk使用的话，需要使用双百分号（%%）来表示一个百分号%。<br />
<br />
如果我能确定一个脚本不需要被指出是使用在Unix, DOS或者CMD环境下，我通常<br />
会省略引号。如果一个例子是GNU awk所特有的，将会用 'gawk'命令来代替。如<br />
果你发现错误或者新的命令想要添加到这个列表里（总长度控制在65个字符一下）<br />
，请通知我。首先我经常会尝试去写一个最短字符的脚本。为了省空间，我通常使用<br />
'1'来代替'{print}'去打印每行，两种写法都能正确运行。<br />
<br />
文本间隔：<br />
<br />
 # 每行后面增加一行空行<br />
 awk '1;{print ""}'<br />
 awk 'BEGIN{ORS="\n\n"};1'<br />
<br />
 # 每行后面增加一行空行。输出文件不会包含连续的两个或两个以上的空行 <br />
 # 注意：在Unix系统， DOS行包括的 CRLF （\r\n） 通常会被作为非空行对待<br />
 # 因此 'NF' 将会返回TRUE。<br />
 awk 'NF{print $0 "\n"}'<br />
<br />
 # 每行后面增加两行空行<br />
 awk '1;{print "\n"}'<br />
<br />
编号和计算：<br />
<br />
 # 以文件为单位，在每句行前加上编号 （左对齐）<br />
 # 使用制表符 （\t） 来代替空格可以有效保护页变的空白。<br />
 awk '{print FNR "\t" $0}' files*<br />
<br />
 # 用制表符 （\t） 给所有文件加上连贯的编号。<br />
 awk '{print NR "\t" $0}' files*<br />
<br />
 # 以文件为单位，在每句行前加上编号 （编号在左，右对齐）<br />
 # 如果在DOS环境下，需要写两个'%'<br />
 awk '{printf("%5d : %s\n", NR,$0)}'<br />
<br />
 # 给非空白行的行加上编号<br />
 # 记得Unix对于 \r 的处理的特殊之处。（上面已经提到）<br />
 awk 'NF{$0=++a " :" $0};{print}'<br />
 awk '{print (NF? ++a " :" :"") $0}'<br />
<br />
 # 计算行数 （模拟 "wc -l"）<br />
 awk 'END{print NR}'<br />
<br />
 # 计算每行每个区域之和<br />
 awk '{s=0; for (i=1; i<=NF; i++) s=s+$i; print s}'<br />
<br />
 # 计算所有行所有区域的总和<br />
 awk '{for (i=1; i<=NF; i++) s=s+$i}; END{print s}'<br />
<br />
 # 打印每行每区域的绝对值<br />
 awk '{for (i=1; i<=NF; i++) if ($i < 0) $i = -$i; print }'<br />
 awk '{for (i=1; i<=NF; i++) $i = ($i < 0) ? -$i : $i; print }'<br />
<br />
 # 计算所有行所有区域（词）的个数<br />
 awk '{ total = total + NF }; END {print total}' file<br />
<br />
 # 打印包含 "Beth" 的行数<br />
 awk '/Beth/{n++}; END {print n+0}' file<br />
<br />
 # 打印第一列最大的行<br />
 # 并且在行前打印出这个最大的数<br />
 awk '$1 > max {max=$1; maxline=$0}; END{ print max, maxline}'<br />
<br />
 # 打印每行的列数，并在后面跟上此行内容<br />
 awk '{ print NF ":" $0 } '<br />
<br />
 # 打印每行的最后一列<br />
 awk '{ print $NF }'<br />
<br />
 # 打印最后一行的最后一列<br />
 awk '{ field = $NF }; END{ print field }'<br />
<br />
 # 打印列数超过4的行<br />
 awk 'NF > 4'<br />
<br />
 # 打印最后一列大于4的行<br />
 awk '$NF > 4'<br />
<br />
构建字符串：<br />
<br />
 # 构建一指定长度的字符串（比如，513个空格）<br />
 awk 'BEGIN{while (a++<513) s=s " "; print s}'<br />
<br />
 # 在某一位置中插入以特定长度的字符串<br />
 # 例子：在每行第6列后插入49个空格<br />
 gawk --re-interval 'BEGIN{while(a++<49)s=s " "};{sub(/^.{6}/,"&" s)};1'<br />
<br />
构建数组：<br />
<br />
 # 以下两个部分并不是一句话脚本，但是这些技巧相当便捷所以也包括进来<br />
 <br />
 # 构建一个叫"month"的数组，以数字为索引，month[1]就是'Jan'，month[2]就是<br />
 # 'Feb'，month[3]就是'Mar'，以此类推。<br />
 split("Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec", month, " ")<br />
<br />
 # 构建一个叫"mdigit"的数组，以字符串为索引，mdigit["Jan"] 等于 1，<br />
 # mdigit["Feb"] 等于 2，等等。需要有"month"数组。<br />
 for (i=1; i<=12; i++) m_digit[month[i]] = i<br />
<br />
<br />
文本转换和替代：<br />
<br />
 # 在Unix环境：转换DOS新行 （CR/LF） 为Unix格式<br />
 awk '{sub(/\r$/,"")};1'   # 假设每行都以Ctrl-M结尾<br />
<br />
 # 在Unix环境：转换Unix新行 （LF） 为DOS格式<br />
 awk '{sub(/$/,"\r")};1'<br />
<br />
 # 在DOS环境：转换Unix新行 （LF） 为DOS格式<br />
 awk 1<br />
<br />
 # 在DOS环境：转换DOS新行 （CR/LF） 为Unix格式<br />
 # DOS版本的awk不能运行, 只能用gawk:<br />
 gawk -v BINMODE="w" '1' infile >outfile<br />
<br />
 # 用 "tr" 替代的方法。<br />
 tr -d \r <infile >outfile            # GNU tr 版本为 1.22 或者更高<br />
<br />
 # 删除每行前的空白（包括空格符和制表符）<br />
 # 使所有文本左对齐<br />
 awk '{sub(/^[ \t]+/, "")};1'<br />
<br />
 # 删除每行结尾的空白（包括空格符和制表符）<br />
 awk '{sub(/[ \t]+$/, "")};1'<br />
<br />
 # 删除每行开头和结尾的所有空白（包括空格符和制表符）<br />
 awk '{gsub(/^[ \t]+|[ \t]+$/,"")};1'<br />
 awk '{$1=$1};1'           # 每列之间的空白也被删除<br />
<br />
 # 在每一行开头处插入5个空格 （做整页的左位移）<br />
 awk '{sub(/^/, "     ")};1'<br />
<br />
 # 用79个字符为宽度，将全部文本右对齐<br />
 awk '{printf "%79s\n", $0}' file*<br />
<br />
 # 用79个字符为宽度，将全部文本居中对齐<br />
 awk '{l=length();s=int((79-l)/2); printf "%"(s+l)"s\n",$0}' file*<br />
<br />
 # 每行用 "bar" 查找替换 "foo"<br />
 awk '{sub(/foo/,"bar")}; 1'           # 仅仅替换第一个找到的"foo"<br />
 gawk '{$0=gensub(/foo/,"bar",4)}; 1'  # 仅仅替换第四个找到的"foo"<br />
 awk '{gsub(/foo/,"bar")}; 1'          # 全部替换<br />
<br />
 # 在包含 "baz" 的行里，将 "foo" 替换为 "bar"<br />
 awk '/baz/{gsub(/foo/, "bar")}; 1'<br />
<br />
 # 在不包含 "baz" 的行里，将 "foo" 替换为 "bar"<br />
 awk '!/baz/{gsub(/foo/, "bar")}; 1'<br />
<br />
 # 将 "scarlet" 或者 "ruby" 或者 "puce" 替换为 "red"<br />
 awk '{gsub(/scarlet|ruby|puce/, "red")}; 1'<br />
<br />
 # 倒排文本 （模拟 "tac"）<br />
 awk '{a[i++]=$0} END {for (j=i-1; j>=0;) print a[j--] }' file*<br />
<br />
 # 如果一行结尾为反斜线符，将下一行接到这行后面<br />
 # （如果有连续多行后面带反斜线符，将会失败）<br />
 awk '/\\$/ {sub(/\\$/,""); getline t; print $0 t; next}; 1' file*<br />
<br />
 # 排序并打印所有登录用户的姓名<br />
 awk -F ":" '{ print $1 | "sort" }' /etc/passwd<br />
<br />
 # 以相反的顺序打印出每行的前两列<br />
 awk '{print $2, $1}' file<br />
<br />
 # 调换前两列的位置<br />
 awk '{temp = $1; $1 = $2; $2 = temp}' file<br />
<br />
 # 打印每行，并删除第二列<br />
 awk '{ $2 = ""; print }'<br />
<br />
 # 倒置每行并打印<br />
 awk '{for (i=NF; i>0; i--) printf("%s ",i);printf ("\n")}' file<br />
<br />
 # 用逗号链接每5行<br />
 awk 'ORS=NR%5?",":"\n"' file <br />
<br />
选择性的打印某些行：<br />
<br />
 # 打印文件的前十行 （模拟 "head"）<br />
 awk 'NR < 11'<br />
<br />
 # 打印文件的第一行 （模拟 "head -1"）<br />
 awk 'NR>1{exit};1'<br />
<br />
  # 打印文件的最后两行 （模拟 "tail -2"）<br />
 awk '{y=x "\n" $0; x=$0};END{print y}'<br />
<br />
 # 打印文件的最后一行 （模拟 "tail -1"）<br />
 awk 'END{print}'<br />
<br />
 # 打印匹配正则表达式的行 （模拟 "grep"）<br />
 awk '/regex/'<br />
<br />
 # 打印不匹配正则表达式的行 （模拟 "grep -v"）<br />
 awk '!/regex/'<br />
<br />
 # 打印第5列等于"abc123"的行<br />
 awk '$5 == "abc123"'<br />
<br />
 # 打印第5列不等于"abc123"的行<br />
 # 这个同样可以用于打印少于5列的行<br />
 awk '$5 != "abc123"'<br />
 awk '!($5 == "abc123")'<br />
<br />
 # 用正则匹配某一列<br />
 awk '$7  ~ /^[a-f]/'    # 打印第7列匹配的行<br />
 awk '$7 !~ /^[a-f]/'    # 打印第7列不匹配的行<br />
<br />
<br />
 # 打印匹配正则表达式的前一行，但是不打印当前行<br />
 awk '/regex/{print x};{x=$0}'<br />
 awk '/regex/{print (x=="" ? "match on line 1" : x)};{x=$0}'<br />
<br />
 # 打印匹配正则表达式的后一行，但是不打印当前行<br />
 awk '/regex/{getline;print}'<br />
<br />
 # 以任何顺序查找包含 AAA、BBB 和 CCC 的行<br />
 awk '/AAA/; /BBB/; /CCC/'<br />
<br />
 # 以指定顺序查找包含 AAA、BBB 和 CCC 的行<br />
 awk '/AAA.*BBB.*CCC/'<br />
<br />
 # 打印长度大于64个字节的行<br />
 awk 'length > 64'<br />
<br />
 # 打印长度小于64个字节的行<br />
 awk 'length < 64'<br />
<br />
 # 打印从匹配正则起到文件末尾的内容<br />
 awk '/regex/,0'<br />
 awk '/regex/,EOF'<br />
<br />
 # 打印指定行之间的内容 （8-12行, 包括第8和第12行）<br />
 awk 'NR==8,NR==12'<br />
<br />
 # 打印第52行<br />
 awk 'NR==52'<br />
 awk 'NR==52 {print;exit}'          # 对于大文件更有效率<br />
<br />
 # 打印两个正则匹配间的内容 （包括正则的内容）<br />
 awk '/Iowa/,/Montana/'             # 大小写敏感<br />
<br />
<br />
选择性的删除某些行：<br />
<br />
 # 删除所有空白行 （类似于 "grep '.' "）<br />
 awk NF<br />
 awk '/./'<br />
<br />
 # 删除重复连续的行 （模拟 "uniq"）<br />
 awk 'a !~ $0; {a=$0}'<br />
<br />
 # 删除重复的、非连续的行<br />
 awk '! a[$0]++'                     # 最简练<br />
 awk '!($0 in a) {a[$0];print}'      # 最有效<br />
<br />
致谢：<br />
<br />
特别感谢 Peter S. Tillier 帮助我完成了一版的FAQ。同样感谢Daniel Jana、<br />
董一粟和其他提出建议和修正的人。<br />
<br />
<br />
更多的语法指令，包括用文件脚本来使用awk，可以参考如下：<br />
<br />
"sed & awk, 2nd Edition," by Dale Dougherty and Arnold Robbins<br />
  O'Reilly, 1997<br />
"UNIX Text Processing," by Dale Dougherty and Tim O'Reilly<br />
  Hayden Books, 1987<br />
  "GAWK: Effective awk Programming," 3d edition, by Arnold D. Robbins<br />
  (O'Reilly, 2003) 或者网址 http://www.gnu.org/software/gawk/manual/<br />
<br />
为了能开发awk的强大功能，开发人员必须知道 "正则表达式"。关于正则<br />
的详细讨论，可以查看 <br />
"Mastering Regular Expressions, 3d edition" by Jeffrey Friedl (O'Reilly,<br />
2006).<br />
<br />
在Unix系统下info和帮助手册("man")会非常有帮助（尝试使用"man awk"、<br />
"man nawk"、"man gawk"、"man regexp"或者"man ed"里面的正则表达式章节）。<br />
<br />
在awk脚本里面使用 '\t'：为了使文档变得清晰，我们在脚本里使用了表达式'\t'<br />
来显示一个制表符 （0x99）。所有awk的版本，甚至UNIX系统7的版本应该能认出<br />
'\t'这种缩写。<br />
<br />
#---文件结尾---
    </td>
  </tr>
  <tr>
    <td>
      <br /><br />
      原文： <a href='http://nullget.sourceforge.net/?q=node/280'>http://nullget.sourceforge.net/?q=node/280</a>
    </td>
  </tr>

</table>

<p></p>
<p></p>
<p></p>
<table width="60%"><tr><td  align="center">
  <div>
    <span>Powered by <a href="http://nullget.sourceforge.net">zexport</a></span>  </div>
  </div>
  </td></tr></table>

<br />
<div>
<script>
  (function(i,s,o,g,r,a,m){i['GoogleAnalyticsObject']=r;i[r]=i[r]||function(){
  (i[r].q=i[r].q||[]).push(arguments)},i[r].l=1*new Date();a=s.createElement(o),
  m=s.getElementsByTagName(o)[0];a.async=1;a.src=g;m.parentNode.insertBefore(a,m)
  })(window,document,'script','//www.google-analytics.com/analytics.js','ga');

  ga('create', 'UA-51467019-1', 'qtchina.github.io');
  ga('send', 'pageview');

</script>

</div>
</body>
</html>
