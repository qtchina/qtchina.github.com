<!DOCTYPE HTML>
<html>
<head>
<meta charset='utf8'>
<title>HANDY ONE-LINERS FOR SED (Unix stream editor)</title>
<link rel="stylesheet" href="../css/base.css" type="text/css" />
</head>
<body>

<table width="80%" height="80 px" align="center">
  <tr>
    <td>
      <div id="branding"><strong><a href="/">
        <img src="../images/nullget_labs.gif" alt="www.qtchina.net " title="www.qtchina.net " id="logo" />
        <span>www.qtchina.net</span>            </a></strong></div>
      </div>
    </td>
  </tr>
</table>

<table width="80%" align="center">
  <tr>
    <td>
      <h2>HANDY ONE-LINERS FOR SED (Unix stream editor)</h2>
    </td>
  </tr>

  <tr>
    <td>
      发布： 2009-02-20 09:17
    </td>
  </tr>

  <tr>
    <td width="50%">
      <br />
-------------------------------------------------------------------------<br />
HANDY ONE-LINERS FOR SED (Unix stream editor)               Apr. 26, 2004<br />
compiled by Eric Pement - pemente[at]northpark[dot]edu        version 5.4<br />
Latest version of this file is usually at:<br />
   http://sed.sourceforge.net/sed1line.txt<br />
   http://www.student.northpark.edu/pemente/sed/sed1line.txt<br />
This file is also available in Portuguese at:<br />
   http://www.lrv.ufsc.br/wmaker/sed_ptBR.html<br />
<br />
FILE SPACING:<br />
<br />
 # double space a file<br />
 sed G<br />
<br />
 # double space a file which already has blank lines in it. Output file<br />
 # should contain no more than one blank line between lines of text.<br />
 sed '/^$/d;G'<br />
<br />
 # triple space a file<br />
 sed 'G;G'<br />
<br />
 # undo double-spacing (assumes even-numbered lines are always blank)<br />
 sed 'n;d'<br />
<br />
 # insert a blank line above every line which matches "regex"<br />
 sed '/regex/{x;p;x;}'<br />
<br />
 # insert a blank line below every line which matches "regex"<br />
 sed '/regex/G'<br />
<br />
 # insert a blank line above and below every line which matches "regex"<br />
 sed '/regex/{x;p;x;G;}'<br />
<br />
NUMBERING:<br />
<br />
 # number each line of a file (simple left alignment). Using a tab (see<br />
 # note on '\t' at end of file) instead of space will preserve margins.<br />
 sed = filename | sed 'N;s/\n/\t/'<br />
<br />
 # number each line of a file (number on left, right-aligned)<br />
 sed = filename | sed 'N; s/^/     /; s/ *\(.\{6,\}\)\n/\1  /'<br />
<br />
 # number each line of file, but only print numbers if line is not blank<br />
 sed '/./=' filename | sed '/./N; s/\n/ /'<br />
<br />
 # count lines (emulates "wc -l")<br />
 sed -n '$='<br />
<br />
TEXT CONVERSION AND SUBSTITUTION:<br />
<br />
 # IN UNIX ENVIRONMENT: convert DOS newlines (CR/LF) to Unix format<br />
 sed 's/.$//'               # assumes that all lines end with CR/LF<br />
 sed 's/^M$//'              # in bash/tcsh, press Ctrl-V then Ctrl-M<br />
 sed 's/\x0D$//'            # gsed 3.02.80, but top script is easier<br />
<br />
 # IN UNIX ENVIRONMENT: convert Unix newlines (LF) to DOS format<br />
 sed "s/$/`echo -e \\\r`/"            # command line under ksh<br />
 sed 's/$'"/`echo \\\r`/"             # command line under bash<br />
 sed "s/$/`echo \\\r`/"               # command line under zsh<br />
 sed 's/$/\r/'                        # gsed 3.02.80<br />
<br />
 # IN DOS ENVIRONMENT: convert Unix newlines (LF) to DOS format<br />
 sed "s/$//"                          # method 1<br />
 sed -n p                             # method 2<br />
<br />
 # IN DOS ENVIRONMENT: convert DOS newlines (CR/LF) to Unix format<br />
 # Can only be done with UnxUtils sed, version 4.0.7 or higher.<br />
 # Cannot be done with other DOS versions of sed. Use "tr" instead.<br />
 sed "s/\r//" infile >outfile         # UnxUtils sed v4.0.7 or higher<br />
 tr -d \r <infile >outfile            # GNU tr version 1.22 or higher<br />
<br />
 # delete leading whitespace (spaces, tabs) from front of each line<br />
 # aligns all text flush left<br />
 sed 's/^[ \t]*//'                    # see note on '\t' at end of file<br />
<br />
 # delete trailing whitespace (spaces, tabs) from end of each line<br />
 sed 's/[ \t]*$//'                    # see note on '\t' at end of file<br />
<br />
 # delete BOTH leading and trailing whitespace from each line<br />
 sed 's/^[ \t]*//;s/[ \t]*$//'<br />
<br />
 # insert 5 blank spaces at beginning of each line (make page offset)<br />
 sed 's/^/     /'<br />
<br />
 # align all text flush right on a 79-column width<br />
 sed -e :a -e 's/^.\{1,78\}$/ &/;ta'  # set at 78 plus 1 space<br />
<br />
 # center all text in the middle of 79-column width. In method 1,<br />
 # spaces at the beginning of the line are significant, and trailing<br />
 # spaces are appended at the end of the line. In method 2, spaces at<br />
 # the beginning of the line are discarded in centering the line, and<br />
 # no trailing spaces appear at the end of lines.<br />
 sed  -e :a -e 's/^.\{1,77\}$/ & /;ta'                     # method 1<br />
 sed  -e :a -e 's/^.\{1,77\}$/ &/;ta' -e 's/\( *\)\1/\1/'  # method 2<br />
<br />
 # substitute (find and replace) "foo" with "bar" on each line<br />
 sed 's/foo/bar/'             # replaces only 1st instance in a line<br />
 sed 's/foo/bar/4'            # replaces only 4th instance in a line<br />
 sed 's/foo/bar/g'            # replaces ALL instances in a line<br />
 sed 's/\(.*\)foo\(.*foo\)/\1bar\2/' # replace the next-to-last case<br />
 sed 's/\(.*\)foo/\1bar/'            # replace only the last case<br />
<br />
 # substitute "foo" with "bar" ONLY for lines which contain "baz"<br />
 sed '/baz/s/foo/bar/g'<br />
<br />
 # substitute "foo" with "bar" EXCEPT for lines which contain "baz"<br />
 sed '/baz/!s/foo/bar/g'<br />
<br />
 # change "scarlet" or "ruby" or "puce" to "red"<br />
 sed 's/scarlet/red/g;s/ruby/red/g;s/puce/red/g'   # most seds<br />
 gsed 's/scarlet\|ruby\|puce/red/g'                # GNU sed only<br />
<br />
 # reverse order of lines (emulates "tac")<br />
 # bug/feature in HHsed v1.5 causes blank lines to be deleted<br />
 sed '1!G;h;$!d'               # method 1<br />
 sed -n '1!G;h;$p'             # method 2<br />
<br />
 # reverse each character on the line (emulates "rev")<br />
 sed '/\n/!G;s/\(.\)\(.*\n\)/&\2\1/;//D;s/.//'<br />
<br />
 # join pairs of lines side-by-side (like "paste")<br />
 sed '$!N;s/\n/ /'<br />
<br />
 # if a line ends with a backslash, append the next line to it<br />
 sed -e :a -e '/\\$/N; s/\\\n//; ta'<br />
<br />
 # if a line begins with an equal sign, append it to the previous line<br />
 # and replace the "=" with a single space<br />
 sed -e :a -e '$!N;s/\n=/ /;ta' -e 'P;D'<br />
<br />
 # add commas to numeric strings, changing "1234567" to "1,234,567"<br />
 gsed ':a;s/\B[0-9]\{3\}\>/,&/;ta'                     # GNU sed<br />
 sed -e :a -e 's/\(.*[0-9]\)\([0-9]\{3\}\)/\1,\2/;ta'  # other seds<br />
<br />
 # add commas to numbers with decimal points and minus signs (GNU sed)<br />
 gsed ':a;s/\(^\|[^0-9.]\)\([0-9]\+\)\([0-9]\{3\}\)/\1\2,\3/g;ta'<br />
<br />
 # add a blank line every 5 lines (after lines 5, 10, 15, 20, etc.)<br />
 gsed '0~5G'                  # GNU sed only<br />
 sed 'n;n;n;n;G;'             # other seds<br />
<br />
SELECTIVE PRINTING OF CERTAIN LINES:<br />
<br />
 # print first 10 lines of file (emulates behavior of "head")<br />
 sed 10q<br />
<br />
 # print first line of file (emulates "head -1")<br />
 sed q<br />
<br />
 # print the last 10 lines of a file (emulates "tail")<br />
 sed -e :a -e '$q;N;11,$D;ba'<br />
<br />
 # print the last 2 lines of a file (emulates "tail -2")<br />
 sed '$!N;$!D'<br />
<br />
 # print the last line of a file (emulates "tail -1")<br />
 sed '$!d'                    # method 1<br />
 sed -n '$p'                  # method 2<br />
<br />
 # print only lines which match regular expression (emulates "grep")<br />
 sed -n '/regexp/p'           # method 1<br />
 sed '/regexp/!d'             # method 2<br />
<br />
 # print only lines which do NOT match regexp (emulates "grep -v")<br />
 sed -n '/regexp/!p'          # method 1, corresponds to above<br />
 sed '/regexp/d'              # method 2, simpler syntax<br />
<br />
 # print the line immediately before a regexp, but not the line<br />
 # containing the regexp<br />
 sed -n '/regexp/{g;1!p;};h'<br />
<br />
 # print the line immediately after a regexp, but not the line<br />
 # containing the regexp<br />
 sed -n '/regexp/{n;p;}'<br />
<br />
 # print 1 line of context before and after regexp, with line number<br />
 # indicating where the regexp occurred (similar to "grep -A1 -B1")<br />
 sed -n -e '/regexp/{=;x;1!p;g;$!N;p;D;}' -e h<br />
<br />
 # grep for AAA and BBB and CCC (in any order)<br />
 sed '/AAA/!d; /BBB/!d; /CCC/!d'<br />
<br />
 # grep for AAA and BBB and CCC (in that order)<br />
 sed '/AAA.*BBB.*CCC/!d'<br />
<br />
 # grep for AAA or BBB or CCC (emulates "egrep")<br />
 sed -e '/AAA/b' -e '/BBB/b' -e '/CCC/b' -e d    # most seds<br />
 gsed '/AAA\|BBB\|CCC/!d'                        # GNU sed only<br />
<br />
 # print paragraph if it contains AAA (blank lines separate paragraphs)<br />
 # HHsed v1.5 must insert a 'G;' after 'x;' in the next 3 scripts below<br />
 sed -e '/./{H;$!d;}' -e 'x;/AAA/!d;'<br />
<br />
 # print paragraph if it contains AAA and BBB and CCC (in any order)<br />
 sed -e '/./{H;$!d;}' -e 'x;/AAA/!d;/BBB/!d;/CCC/!d'<br />
<br />
 # print paragraph if it contains AAA or BBB or CCC<br />
 sed -e '/./{H;$!d;}' -e 'x;/AAA/b' -e '/BBB/b' -e '/CCC/b' -e d<br />
 gsed '/./{H;$!d;};x;/AAA\|BBB\|CCC/b;d'         # GNU sed only<br />
<br />
 # print only lines of 65 characters or longer<br />
 sed -n '/^.\{65\}/p'<br />
<br />
 # print only lines of less than 65 characters<br />
 sed -n '/^.\{65\}/!p'        # method 1, corresponds to above<br />
 sed '/^.\{65\}/d'            # method 2, simpler syntax<br />
<br />
 # print section of file from regular expression to end of file<br />
 sed -n '/regexp/,$p'<br />
<br />
 # print section of file based on line numbers (lines 8-12, inclusive)<br />
 sed -n '8,12p'               # method 1<br />
 sed '8,12!d'                 # method 2<br />
<br />
 # print line number 52<br />
 sed -n '52p'                 # method 1<br />
 sed '52!d'                   # method 2<br />
 sed '52q;d'                  # method 3, efficient on large files<br />
<br />
 # beginning at line 3, print every 7th line<br />
 gsed -n '3~7p'               # GNU sed only<br />
 sed -n '3,${p;n;n;n;n;n;n;}' # other seds<br />
<br />
 # print section of file between two regular expressions (inclusive)<br />
 sed -n '/Iowa/,/Montana/p'             # case sensitive<br />
<br />
SELECTIVE DELETION OF CERTAIN LINES:<br />
<br />
 # print all of file EXCEPT section between 2 regular expressions<br />
 sed '/Iowa/,/Montana/d'<br />
<br />
 # delete duplicate, consecutive lines from a file (emulates "uniq").<br />
 # First line in a set of duplicate lines is kept, rest are deleted.<br />
 sed '$!N; /^\(.*\)\n\1$/!P; D'<br />
<br />
 # delete duplicate, nonconsecutive lines from a file. Beware not to<br />
 # overflow the buffer size of the hold space, or else use GNU sed.<br />
 sed -n 'G; s/\n/&&/; /^\([ -~]*\n\).*\n\1/d; s/\n//; h; P'<br />
<br />
 # delete all lines except duplicate lines (emulates "uniq -d").<br />
 sed '$!N; s/^\(.*\)\n\1$/\1/; t; D'<br />
<br />
 # delete the first 10 lines of a file<br />
 sed '1,10d'<br />
<br />
 # delete the last line of a file<br />
 sed '$d'<br />
<br />
 # delete the last 2 lines of a file<br />
 sed 'N;$!P;$!D;$d'<br />
<br />
 # delete the last 10 lines of a file<br />
 sed -e :a -e '$d;N;2,10ba' -e 'P;D'   # method 1<br />
 sed -n -e :a -e '1,10!{P;N;D;};N;ba'  # method 2<br />
<br />
 # delete every 8th line<br />
 gsed '0~8d'                           # GNU sed only<br />
 sed 'n;n;n;n;n;n;n;d;'                # other seds<br />
<br />
 # delete ALL blank lines from a file (same as "grep '.' ")<br />
 sed '/^$/d'                           # method 1<br />
 sed '/./!d'                           # method 2<br />
<br />
 # delete all CONSECUTIVE blank lines from file except the first; also<br />
 # deletes all blank lines from top and end of file (emulates "cat -s")<br />
 sed '/./,/^$/!d'          # method 1, allows 0 blanks at top, 1 at EOF<br />
 sed '/^$/N;/\n$/D'        # method 2, allows 1 blank at top, 0 at EOF<br />
<br />
 # delete all CONSECUTIVE blank lines from file except the first 2:<br />
 sed '/^$/N;/\n$/N;//D'<br />
<br />
 # delete all leading blank lines at top of file<br />
 sed '/./,$!d'<br />
<br />
 # delete all trailing blank lines at end of file<br />
 sed -e :a -e '/^\n*$/{$d;N;ba' -e '}'  # works on all seds<br />
 sed -e :a -e '/^\n*$/N;/\n$/ba'        # ditto, except for gsed 3.02*<br />
<br />
 # delete the last line of each paragraph<br />
 sed -n '/^$/{p;h;};/./{x;/./p;}'<br />
<br />
SPECIAL APPLICATIONS:<br />
<br />
 # remove nroff overstrikes (char, backspace) from man pages. The 'echo'<br />
 # command may need an -e switch if you use Unix System V or bash shell.<br />
 sed "s/.`echo \\\b`//g"    # double quotes required for Unix environment<br />
 sed 's/.^H//g'             # in bash/tcsh, press Ctrl-V and then Ctrl-H<br />
 sed 's/.\x08//g'           # hex expression for sed v1.5<br />
<br />
 # get Usenet/e-mail message header<br />
 sed '/^$/q'                # deletes everything after first blank line<br />
<br />
 # get Usenet/e-mail message body<br />
 sed '1,/^$/d'              # deletes everything up to first blank line<br />
<br />
 # get Subject header, but remove initial "Subject: " portion<br />
 sed '/^Subject: */!d; s///;q'<br />
<br />
 # get return address header<br />
 sed '/^Reply-To:/q; /^From:/h; /./d;g;q'<br />
<br />
 # parse out the address proper. Pulls out the e-mail address by itself<br />
 # from the 1-line return address header (see preceding script)<br />
 sed 's/ *(.*)//; s/>.*//; s/.*[:<] *//'<br />
<br />
 # add a leading angle bracket and space to each line (quote a message)<br />
 sed 's/^/> /'<br />
<br />
 # delete leading angle bracket & space from each line (unquote a message)<br />
 sed 's/^> //'<br />
<br />
 # remove most HTML tags (accommodates multiple-line tags)<br />
 sed -e :a -e 's/<[^>]*>//g;/</N;//ba'<br />
<br />
 # extract multi-part uuencoded binaries, removing extraneous header<br />
 # info, so that only the uuencoded portion remains. Files passed to<br />
 # sed must be passed in the proper order. Version 1 can be entered<br />
 # from the command line; version 2 can be made into an executable<br />
 # Unix shell script. (Modified from a script by Rahul Dhesi.)<br />
 sed '/^end/,/^begin/d' file1 file2 ... fileX | uudecode   # vers. 1<br />
 sed '/^end/,/^begin/d' "$@" | uudecode                    # vers. 2<br />
<br />
 # zip up each .TXT file individually, deleting the source file and<br />
 # setting the name of each .ZIP file to the basename of the .TXT file<br />
 # (under DOS: the "dir /b" switch returns bare filenames in all caps).<br />
 echo @echo off >zipup.bat<br />
 dir /b *.txt | sed "s/^\(.*\)\.TXT/pkzip -mo \1 \1.TXT/" >>zipup.bat<br />
<br />
TYPICAL USE: Sed takes one or more editing commands and applies all of<br />
them, in sequence, to each line of input. After all the commands have<br />
been applied to the first input line, that line is output and a second<br />
input line is taken for processing, and the cycle repeats. The<br />
preceding examples assume that input comes from the standard input<br />
device (i.e, the console, normally this will be piped input). One or<br />
more filenames can be appended to the command line if the input does<br />
not come from stdin. Output is sent to stdout (the screen). Thus:<br />
<br />
 cat filename | sed '10q'        # uses piped input<br />
 sed '10q' filename              # same effect, avoids a useless "cat"<br />
 sed '10q' filename > newfile    # redirects output to disk<br />
<br />
For additional syntax instructions, including the way to apply editing<br />
commands from a disk file instead of the command line, consult "sed &<br />
awk, 2nd Edition," by Dale Dougherty and Arnold Robbins (O'Reilly,<br />
1997; http://www.ora.com), "UNIX Text Processing," by Dale Dougherty<br />
and Tim O'Reilly (Hayden Books, 1987) or the tutorials by Mike Arst<br />
distributed in U-SEDIT2.ZIP (many sites). To fully exploit the power<br />
of sed, one must understand "regular expressions." For this, see<br />
"Mastering Regular Expressions" by Jeffrey Friedl (O'Reilly, 1997).<br />
The manual ("man") pages on Unix systems may be helpful (try "man<br />
sed", "man regexp", or the subsection on regular expressions in "man<br />
ed"), but man pages are notoriously difficult. They are not written to<br />
teach sed use or regexps to first-time users, but as a reference text<br />
for those already acquainted with these tools.<br />
<br />
QUOTING SYNTAX: The preceding examples use single quotes ('...')<br />
instead of double quotes ("...") to enclose editing commands, since<br />
sed is typically used on a Unix platform. Single quotes prevent the<br />
Unix shell from intrepreting the dollar sign ($) and backquotes<br />
(`...`), which are expanded by the shell if they are enclosed in<br />
double quotes. Users of the "csh" shell and derivatives will also need<br />
to quote the exclamation mark (!) with the backslash (i.e., \!) to<br />
properly run the examples listed above, even within single quotes.<br />
Versions of sed written for DOS invariably require double quotes<br />
("...") instead of single quotes to enclose editing commands.<br />
<br />
USE OF '\t' IN SED SCRIPTS: For clarity in documentation, we have used<br />
the expression '\t' to indicate a tab character (0x09) in the scripts.<br />
However, most versions of sed do not recognize the '\t' abbreviation,<br />
so when typing these scripts from the command line, you should press<br />
the TAB key instead. '\t' is supported as a regular expression<br />
metacharacter in awk, perl, and HHsed, sedmod, and GNU sed v3.02.80.<br />
<br />
VERSIONS OF SED: Versions of sed do differ, and some slight syntax<br />
variation is to be expected. In particular, most do not support the<br />
use of labels (:name) or branch instructions (b,t) within editing<br />
commands, except at the end of those commands. We have used the syntax<br />
which will be portable to most users of sed, even though the popular<br />
GNU versions of sed allow a more succinct syntax. When the reader sees<br />
a fairly long command such as this:<br />
<br />
   sed -e '/AAA/b' -e '/BBB/b' -e '/CCC/b' -e d<br />
<br />
it is heartening to know that GNU sed will let you reduce it to:<br />
<br />
   sed '/AAA/b;/BBB/b;/CCC/b;d'      # or even<br />
   sed '/AAA\|BBB\|CCC/b;d'<br />
<br />
In addition, remember that while many versions of sed accept a command<br />
like "/one/ s/RE1/RE2/", some do NOT allow "/one/! s/RE1/RE2/", which<br />
contains space before the 's'. Omit the space when typing the command.<br />
<br />
OPTIMIZING FOR SPEED: If execution speed needs to be increased (due to<br />
large input files or slow processors or hard disks), substitution will<br />
be executed more quickly if the "find" expression is specified before<br />
giving the "s/.../.../" instruction. Thus:<br />
<br />
   sed 's/foo/bar/g' filename         # standard replace command<br />
   sed '/foo/ s/foo/bar/g' filename   # executes more quickly<br />
   sed '/foo/ s//bar/g' filename      # shorthand sed syntax<br />
<br />
On line selection or deletion in which you only need to output lines<br />
from the first part of the file, a "quit" command (q) in the script<br />
will drastically reduce processing time for large files. Thus:<br />
<br />
   sed -n '45,50p' filename           # print line nos. 45-50 of a file<br />
   sed -n '51q;45,50p' filename       # same, but executes much faster<br />
<br />
If you have any additional scripts to contribute or if you find errors<br />
in this document, please send e-mail to the compiler. Indicate the<br />
version of sed you used, the operating system it was compiled for, and<br />
the nature of the problem. Various scripts in this file were written<br />
or contributed by:<br />
<br />
 Al Aab <af137@freenet.toronto.on.ca>   # "seders" list moderator<br />
 Edgar Allen <era@sky.net>              # various<br />
 Yiorgos Adamopoulos <adamo@softlab.ece.ntua.gr><br />
 Dale Dougherty <dale@songline.com>     # author of "sed & awk"<br />
 Carlos Duarte <cdua@algos.inesc.pt>    # author of "do it with sed"<br />
 Eric Pement <pemente@northpark.edu>    # author of this document<br />
 Ken Pizzini <ken@halcyon.com>          # author of GNU sed v3.02<br />
 S.G. Ravenhall <stew.ravenhall@totalise.co.uk> # great de-html script<br />
 Greg Ubben <gsu@romulus.ncsc.mil>      # many contributions & much help<br />
-------------------------------------------------------------------------
    </td>
  </tr>
  <tr>
    <td>
      <br /><br />
      原文： <a href='http://nullget.sourceforge.net/?q=node/281'>http://nullget.sourceforge.net/?q=node/281</a>
    </td>
  </tr>

</table>

<p></p>
<p></p>
<p></p>
<table width="60%"><tr><td  align="center">
<div>
  <span>Powered by <a href="http://nullget.sourceforge.net">zexport</a></span>  </div>
</div>
</td></tr></table>

</body>
</html>
